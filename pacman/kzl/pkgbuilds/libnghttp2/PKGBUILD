# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libnghttp2
pkgver=1.41.0
pkgrel=1
epoch=1
pkgdesc="Framing layer of HTTP/2 is implemented as a reusable C library"
arch=('x86_64')
url="https://nghttp2.org"
source=(https://github.com/nghttp2/nghttp2/releases/download/v$pkgver/nghttp2-$pkgver.tar.xz)
sha256sums=('abc25b8dc601f5b3fefe084ce50fcbdc63e3385621bee0cbfa7b57f9ec3e67c2')

build() {
  cd nghttp2-$pkgver

  autoreconf -ifv

  ./configure \
    --prefix=/usr \
    --enable-lib-only \
    --disable-examples \
    --disable-python-bindings

  make
}

check() {
  cd nghttp2-$pkgver

  make check || warning "Check failed."
}

package() {
  cd nghttp2-$pkgver/lib

  make DESTDIR="$pkgdir/" install
}
