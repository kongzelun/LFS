# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=nano
pkgver=5.6
pkgrel=1
epoch=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
url="https://www.nano-editor.org"
backup=(etc/nanorc)
source=($url/dist/v${pkgver::1}/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('fce183e4a7034d07d219c79aa2f579005d1fd49f156db6e50f53543a87637a32'
            'SKIP')
validpgpkeys=('BFD009061E535052AD0DF2150D28D4D2A0ACE884')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-color \
    --enable-nanorc \
    --enable-multibuffer

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/doc/sample.nanorc "$pkgdir"/etc/nanorc
}
