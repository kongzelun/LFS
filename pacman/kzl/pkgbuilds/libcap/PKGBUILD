# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libcap
pkgver=2.48
pkgrel=1
epoch=1
pkgdesc="POSIX 1003.1e capabilities"
arch=('x86_64')
url="https://sites.google.com/site/fullycapable/"
provides=(libcap.so)
source=(https://kernel.org/pub/linux/libs/security/linux-privs/libcap2/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('4de9590ee09a87c282d558737ffb5b6175ccbfd26d580add10df44d0f047f6c2'
            'SKIP')
validpgpkeys=('38A644698C69787344E954CE29EE848AE2CCF3F4')

prepare() {
  cd $pkgname-$pkgver

  sed -e '/SBINDIR=$(exec_prefix)/s/sbin/bin/' \
      -e '/CFLAGS += -Dlinux/s/-Dlinux/$(CPPFLAGS) -Dlinux/' \
      -i Make.Rules
}

build() {
  cd $pkgname-$pkgver

  make prefix="/usr" lib="lib" SBINDIR="/usr/bin" KERNEL_HEADERS="/usr/include"
}

check() {
  cd $pkgname-$pkgver

  make test || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" prefix="/usr" lib="lib" SBINDIR="/usr/bin" KERNEL_HEADERS="/usr/include" RAISE_SETFCAP="no" install

  install -Dm644 pam_cap/capability.conf -t "$pkgdir"/usr/share/doc/$pkgname/examples/
}
