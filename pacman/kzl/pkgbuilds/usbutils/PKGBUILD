# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=usbutils
pkgver=013
pkgrel=1
epoch=1
pkgdesc="USB Device Utilities"
arch=('x86_64')
depends=(libusb)
source=(https://www.kernel.org/pub/linux/utils/usb/usbutils/$pkgname-$pkgver.tar{.xz,.sign})
sha256sums=('9e23494fcc78b7a80ee29a07dd179c95ae2f71509c35728dbbabc2d1cca41338'
            'SKIP')
validpgpkeys=('647F28654894E3BD457199BE38DBBDC86092693E')

prepare() {
  cd $pkgname-$pkgver

  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --datadir=/usr/share/hwdata \
    --disable-zlib

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
