# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libunwind
pkgver=1.3.1
pkgrel=1
epoch=1
pkgdesc="Portable and efficient C programming interface (API) to determine the call-chain of a program"
arch=('x86_64')
url="https://www.nongnu.org/libunwind"
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('43997a3939b6ccdf2f669b50fdb8a4d3205374728c2923ddc2354c65260214f8'
            'SKIP')
validpgpkeys=('1675C8DA2EF907FB116EB709EC52B396E6874AF2')

build() {
  cd $pkgname-$pkgver

  CFLAGS+=" -fcommon"

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
