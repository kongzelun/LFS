# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=node
pkgver=15.10.0
pkgrel=1
epoch=1
pkgdesc="Evented I/O for V8 javascript"
arch=('x86_64')
url="https://nodejs.org/"
depends=(icu libnghttp2 libuv openssl)
provides=(nodejs npm)
conflicts=(nodejs npm)
replaces=(nodejs npm)
source=(https://github.com/nodejs/node/archive/v$pkgver.tar.gz)
sha256sums=('c4973dfe68ee7ed9681821c96ce41f3a6e73e5572745e597f76536805b2455fb')

build() {
  cd $pkgname-$pkgver

  CFLAGS="${CFLAGS/-O2/-O3}"
  CXXFLAGS="${CXXFLAGS/-O2/-O3}"

  ./configure \
    --prefix=/usr \
    --experimental-http-parser \
    --shared-libuv \
    --shared-nghttp2 \
    --shared-openssl \
    --shared-zlib \
    --with-intl=system-icu

  make
}

check() {
  cd $pkgname-$pkgver

  make test || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
