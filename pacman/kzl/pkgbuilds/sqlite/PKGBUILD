# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=sqlite
pkgver=3340100
pkgrel=1
epoch=1
pkgdesc="A C library that implements an SQL database engine"
arch=('x86_64')
url="https://www.sqlite.org"
makedepends=(tcl)
source=($url/2021/$pkgname-src-$pkgver.zip)
sha256sums=('dddd237996b096dee8b37146c7a37a626a80306d6695103d2ec16ee3b852ff49')

build() {
  cd $pkgname-src-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-amalgamation \
    --enable-fts5 \
    --enable-json1 \
    --enable-rtree

  CFLAGS="$CFLAGS \
    -DSQLITE_SECURE_DELETE \
    -DSQLITE_ENABLE_COLUMN_METADATA \
    -DSQLITE_ENABLE_DBSTAT_VTAB \
    -DSQLITE_ENABLE_UNLOCK_NOTIFY"

  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool

  make
  make showdb showjournal showstat4 showwal sqldiff sqlite3_analyzer
}

package() {
  cd $pkgname-src-$pkgver

  make DESTDIR="$pkgdir/" install
  install -Dm755 showdb showjournal showstat4 showwal sqldiff sqlite3_analyzer "$pkgdir"/usr/bin/
}
