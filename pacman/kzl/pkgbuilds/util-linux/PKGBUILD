# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=util-linux
pkgver=2.36
pkgrel=2
epoch=1
pkgdesc="Miscellaneous system utilities for Linux"
arch=('x86_64')
url="https://github.com/karelzak/util-linux"
depends=(coreutils libcap-ng libxcrypt pam shadow)
optdepends=("words: default dictionary for look")
provides=(util-linux-libs libutil-linux libblkid.so libfdisk.so libmount.so libsmartcols.so libuuid.so)
conflicts=(util-linux-libs libutil-linux)
replaces=(util-linux-libs libutil-linux)
backup=(etc/pam.d/{chfn,chsh,login,runuser,runuser-l,su,su-l})
source=(https://www.kernel.org/pub/linux/utils/util-linux/v$pkgver/$pkgname-$pkgver.$pkgrel.tar.{xz,sign}
        60-rfkill.rules
        pam-{login,common,runuser,su}
        rfkill-block@.service
        rfkill-unblock@.service)
sha256sums=('f7516ba9d8689343594356f0e5e1a5f0da34adfbc89023437735872bb5024c5f'
            'SKIP'
            '7423aaaa09fee7f47baa83df9ea6fef525ff9aec395c8cbd9fe848ceb2643f37'
            'd1c465f449dc2464d15502dc501fc922f037cef65a9e2e5f81c1e0b782b783cf'
            '4902920671abb084ab6301302c62718fe22cdb38563f5f95771d54ac7aa70385'
            '3db80878d889e3321199793c1fe799af72f0a5470787afded05aec485c18376c'
            '5099e99bb656079136bc82c841a7fdbd211dad93bfe4fd707090620dac13aafb'
            'a22e0a037e702170c7d88460cc9c9c2ab1d3e5c54a6985cd4a164ea7beff1b36'
            '8ccec10a22523f6b9d55e0d6cbf91905a39881446710aa083e935e8073323376')
validpgpkeys=('B0C64D14301CC6EFAEDF60E4E4B71D5EEC39C284')

build() {
  cd $pkgname-$pkgver.$pkgrel

  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --libdir=/usr/lib \
    --disable-hardlink \
    --enable-mesg \
    --enable-raw \
    --enable-vipw \
    --enable-newgrp \
    --enable-chfn-chsh \
    --enable-write \
    --enable-fs-paths-default=/usr/bin:/usr/local/bin \
    --enable-usrdir-path \
    --with-python=3

  make
}

package() {
  cd $pkgname-$pkgver.$pkgrel

  make DESTDIR="$pkgdir/" usrsbin_execdir=/usr/bin install

  # setuid chfn, chsh and newgrp
  chmod 4755 "$pkgdir"/usr/bin/{ch{sh,fn},newgrp}

  # install PAM files for login-utils
  install -dm755 "$pkgdir"/etc/pam.d
  install -m644 "$srcdir"/pam-common "$pkgdir"/etc/pam.d/chfn
  install -m644 "$srcdir"/pam-common "$pkgdir"/etc/pam.d/chsh
  install -m644 "$srcdir"/pam-login "$pkgdir"/etc/pam.d/login
  install -m644 "$srcdir"/pam-runuser "$pkgdir"/etc/pam.d/runuser
  install -m644 "$srcdir"/pam-runuser "$pkgdir"/etc/pam.d/runuser-l
  install -m644 "$srcdir"/pam-su "$pkgdir"/etc/pam.d/su
  install -m644 "$srcdir"/pam-su "$pkgdir"/etc/pam.d/su-l

  # install systemd-sysusers
  echo 'u uuidd 68\ng rfkill - - -' | install -Dm644 /dev/stdin "$pkgdir"/usr/lib/sysusers.d/util-linux.conf

  install -Dm0644 "$srcdir"/60-rfkill.rules "$pkgdir"/usr/lib/udev/rules.d/60-rfkill.rules

  install -Dm0644 "$srcdir"/rfkill-block@.service "$pkgdir"/usr/lib/systemd/system/rfkill-block@.service
  install -Dm0644 "$srcdir"/rfkill-unblock@.service "$pkgdir"/usr/lib/systemd/system/rfkill-unblock@.service
}
