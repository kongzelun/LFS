# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=e2fsprogs
pkgver=1.46.1
pkgrel=1
epoch=1
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('x86_64')
url="http://e2fsprogs.sourceforge.net"
backup=(etc/mke2fs.conf)
options=(staticlibs)
source=(https://www.kernel.org/pub/linux/kernel/people/tytso/$pkgname/v$pkgver/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('bbe6af3b7bb7119556b6f57a78d41b330007738bca8530b2ba95d2ba35430151'
            'SKIP')
validpgpkeys=('3AB057B7E78D945C8C5591FBD36F769BC11804F0')

prepare() {
  cd $pkgname-$pkgver

  # Remove unnecessary init.d directory
  sed -i '/init\.d/s|^|#|' misc/Makefile.in
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --sbindir=/usr/bin \
    --with-root-prefix="" \
    --enable-elf-shlibs \
    --disable-fsck \
    --disable-uuidd \
    --disable-libuuid \
    --disable-libblkid

  make

  # regenerate locale files
  find po/ -name '*.gmo' -delete
  make -C po update-gmo
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install install-libs

  sed -e 's/^AWK=.*/AWK=awk/' -i "$pkgdir"/usr/bin/compile_et

  # remove references to build directory
  sed -i -e 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' "$pkgdir"/usr/bin/mk_cmds
  sed -i -e 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' "$pkgdir"/usr/bin/compile_et

  # remove static libraries with a shared counterpart
  rm "$pkgdir"/usr/lib/lib{com_err,e2p,ext2fs,ss}.a
}
