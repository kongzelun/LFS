# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=nettle
pkgver=3.7.1
pkgrel=1
epoch=1
pkgdesc="A low-level cryptographic library"
arch=('x86_64')
url="https://www.lysator.liu.se/~nisse/nettle"
depends=(gmp)
checkdepends=(valgrind)
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('156621427c7b00a75ff9b34b770b95d34f80ef7a55c3407de94b16cbf436c42e'
            'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static

  make
}

check() {
  cd $pkgname-$pkgver

  make -k check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
