# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=openmp
pkgver=11.1.0
pkgrel=1
epoch=1
pkgdesc="LLVM OpenMP Runtime Library"
arch=('x86_64')
url="https://openmp.llvm.org/"
depends=(elfutils libffi)
source=(https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/$pkgname-$pkgver.src.tar.xz{,.sig})
sha256sums=('d187483b75b39acb3ff8ea1b7d98524d95322e3cb148842957e9b0fbb866052e'
            'SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A')

prepare() {
	cd $pkgname-$pkgver.src

	mkdir build
}

build() {
	cd $pkgname-$pkgver.src

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -DLIBOMP_INSTALL_ALIASES=OFF \
    -S . \
    -B build

  cmake --build build
}

check() {
	cd $pkgname-$pkgver.src

  cmake --build build --target tests || warning "Check failed."
}

package() {
	cd $pkgname-$pkgver.src

  cmake --build build --target install -- DESTDIR="$pkgdir/"
}
