# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=boost
pkgver=1.75.0
pkgrel=1
epoch=1
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('x86_64')
url="https://www.boost.org/"
depends=(icu)
makedepends=(openmpi)
optdepends=("openmpi: for mpi support")
provides=(boost-libs)
conflicts=(boost-libs)
replaces=(boost-libs)
source=(https://dl.bintray.com/boostorg/release/$pkgver/source/${pkgname}_${pkgver//./_}.tar.bz2)
sha256sums=('953db31e016db7bb207f11432bef7df100516eeb746843fa0486a222e3fd49cb')

# TODO: Support for OpenMPI

build() {
  cd ${pkgname}_${pkgver//./_}

  ./bootstrap.sh \
    --with-toolset=gcc \
    --with-icu \
    --with-python=/usr/bin/python3 \
    --prefix=/usr

  echo "using mpi ;" >> project-config.jam

  ./b2 \
    --build-dir=build \
    --layout=system \
    toolset=gcc \
    variant=release \
    link=shared,static \
    threading=multi \
    runtime-link=shared \
    cflags="$CPPFLAGS $CFLAGS -fPIC -O3" \
    cxxflags="$CPPFLAGS $CXXFLAGS -std=c++14 -fPIC -O3" \
    linkflags="$LDFLAGS" \
    -d+2 \
    -j$(nproc) \
    stage
}

package() {
  cd ${pkgname}_${pkgver//./_}

  ./b2 \
    --prefix="$pkgdir/usr/" \
    --build-dir=build \
    toolset=gcc \
    variant=release \
    link=shared,static \
    threading=multi \
    runtime-link=shared \
    cflags="$CPPFLAGS $CFLAGS -fPIC -O3" \
    cxxflags="$CPPFLAGS $CXXFLAGS -std=c++14 -fPIC -O3" \
    linkflags="$LDFLAGS" \
    -d+2 \
    -j$(nproc) \
    install
}
