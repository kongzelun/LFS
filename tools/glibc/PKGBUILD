# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> binutils (pass 1) -> gcc (pass 1) -> glibc

pkgname=lfs-tools-glibc
pkgver=2.31
pkgrel=7
arch=('x86_64' 'aarch64')
url="https://www.gnu.org/software/libc"
options=(!strip)
source=(https://ftp.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz{,.sig}
        ld.so.conf)
install=glibc.install
sha256sums=('9246fe44f68feeec8c666bb87973d590ce0137cca145df014c72ec95be9ffd17'
            'SKIP'
            'dad04a370e488aa85fb0a813a5c83cf6fd981ce01883fc59685447b092de84b5')
validpgpkeys=('BC7C7372637EC10C57D7AA6579C43DFBF1CF2187')

prepare() {
  cd glibc-$pkgver
  
  mkdir build
}

build() {
  cd glibc-$pkgver/build

  echo "slibdir=/tools/lib" >> configparms
  echo "rtlddir=/tools/lib" >> configparms
  echo "sbindir=/tools/bin" >> configparms
  echo "rootsbindir=/tools/bin" >> configparms

  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

  CFLAGS=${CFLAGS/-fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fno-plt/}
  LDFLAGS=${LDFLAGS/,-z,now/}

  ../configure \
    --prefix=/tools \
    --sbindir=/tools/bin \
    --libexecdir=/tools/lib \
    --libdir=/tools/lib \
    --with-headers=/tools/include \
    --enable-kernel=5.4.42 \
    --enable-shared \
    --enable-static-pie \
    --enable-cet \
    --disable-profile \
    --disable-timezone-tools \
    --enable-stack-protector=strong \
    --enable-bind-now \
    --with-pkgversion="KZL $pkgver-$pkgrel"
    --host=$CHOST \
    --enable-multi-arch
  
  make
}

check() {
  cd glibc-$pkgver/build

  make check || warning "Check failed."
}

package() {
  cd glibc-$pkgver/build

  make install_root="$pkgdir/" install

  rm -f "$pkgdir"/etc/ld.so.{cache,conf}
  install -Dm644 "$srcdir"/ld.so.conf "$pkgdir"/etc/
  install -dm755 "$pkgdir"/etc/ld.so.conf.d/
}
