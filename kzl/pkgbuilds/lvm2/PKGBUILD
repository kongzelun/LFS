# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=lvm2
pkgname=(lvm2 device-mapper)
pkgver=2.03.11
pkgrel=1
epoch=1
arch=('x86_64')
url="https://sourceware.org/lvm2/"
makedepends=(thin-provisioning-tools)
source=(git+https://sourceware.org/git/$pkgbase.git#tag=v${pkgver//./_}?signed)
sha256sums=('SKIP')
validpgpkeys=('D501A478440AE2FD130A1BE8B9112431E509039F')

prepare() {
  cd $pkgbase

  # remove install section from systemd units that are enabled by default
  sed -i -e '/^\[Install\]$/,$d' \
    scripts/dm_event_systemd_red_hat.socket.in \
    scripts/lvm2_lvmpolld_systemd_red_hat.socket.in \
    scripts/lvm2_monitoring_systemd_red_hat.service.in
}

build() {
  cd $pkgbase

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-applib \
    --enable-cmdlib \
    --enable-dmeventd \
    --enable-lvmetad \
    --enable-lvmpolld \
    --enable-pkgconfig \
    --enable-readline \
    --enable-udev_rules \
    --enable-udev_sync \
    --enable-use-lvmetad \
    --enable-udev-systemd-background-jobs \
    --with-cache=internal \
    --with-default-dm-run-dir=/run \
    --with-default-locking-dir=/run/lock/lvm \
    --with-default-pid-dir=/run \
    --with-default-run-dir=/run/lvm \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-thin=internal \
    --with-udev-prefix=/usr

  make
}

package_device-mapper() {
  pkgdesc="Device mapper userspace library and tools"
  url="http://sourceware.org/dm/"

  cd $pkgbase

  make DESTDIR="$pkgdir/" install_device-mapper
  make DESTDIR="$pkgdir/" install_systemd_units
  rm -f "$pkgdir"/usr/lib/systemd/system/{blk-availability.service,lvm2-*}
  install -d -m0755 "$pkgdir"/usr/lib/systemd/system/sockets.target.wants
  ln -sf ../dm-event.socket "$pkgdir"/usr/lib/systemd/system/sockets.target.wants/dm-event.socket
}

package_lvm2() {
  depends=(device-mapper thin-provisioning-tools)
  backup=(etc/lvm/lvm.conf
          etc/lvm/lvmlocal.conf)

  cd $pkgbase

  make DESTDIR="$pkgdir/" install_lvm2
  make -C liblvm DESTDIR="$pkgdir/" install
  make DESTDIR="$pkgdir/" install_systemd_units
  rm -f "$pkgdir"/usr/lib/systemd/system/dm-*
  make DESTDIR="$pkgdir/" install_systemd_generators
}
