# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=samba
pkgver=4.13.2
pkgrel=1
epoch=1
arch=('x86_64')
url="https://www.samba.org"
depends=(acl db jansson ldb libtirpc popt talloc tdb tevent)
makedepends=(rpcsvc-proto)
backup=(etc/conf.d/samba
        etc/logrotate.d/samba
        etc/pam.d/samba)
source=(https://www.samba.org/samba/ftp/stable/$pkgname-$pkgver.tar.{gz,asc}
        samba.logrotate
        samba.pam
        samba.tmpfiles)
sha256sums=('276464396a05d88b775bda01ac2eb1e5a636ccf7010b0fd28efc3d85583af2b4'
            'SKIP'
            '0b570570ea49f82e6f9da47b1cce3b441859f52de105b54d4d79b873b924623f'
            'a3e28676e9f11136690f6d46ca15b1d548cec2e29db043206ab09582671dc2d5'
            '5398a42ccb36ac3b87cfa42c8050b535efc2ca38ab3b9354a52f4e0fe71b5800')
validpgpkeys=('52FBC0B86D954B0843324CDC6F33915B6568B7EA')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --with-systemd \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/samba \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libdir=/usr/lib \
    --with-configdir=/etc/samba \
    --with-lockdir=/var/cache/samba \
    --with-piddir=/run \
    --with-sockets-dir=/run/samba \
    --bundled-libraries=!ldb,!popt,!talloc,!tdb,!tevent,!pyldb-util,!pytalloc-util \
    --disable-rpath-install \
    --enable-fhs \
    --with-pammodulesdir=/usr/lib/security \
    --systemd-install-services

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  rm -rf "$pkgdir"/run

  # sysconfig
  rm -rf "$pkgdir"/etc/sysconfig
  install -dm755 "$pkgdir"/etc/conf.d
  sed -i -e '/^EnvironmentFile/s/sysconfig/conf.d/' "$pkgdir"/usr/lib/systemd/system/*.service
  install -Dm644 "$srcdir"/samba-$pkgver/packaging/systemd/samba.sysconfig "$pkgdir"/etc/conf.d/samba

  # pam
  install -Dm644 "${srcdir}"/samba.pam "${pkgdir}"/etc/pam.d/samba 

  # tmpfiles
  install -Dm644 "$srcdir"/samba.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/samba.conf

  # logrotate
  install -Dm644 "$srcdir"/samba.logrotate "$pkgdir"/etc/logrotate.d/samba

  # smb.conf example
  install -Dm644 "$srcdir"/samba-$pkgver/examples/smb.conf.default "$pkgdir"/etc/samba/smb.conf.default
}
