# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libgpg-error
pkgver=1.41
pkgrel=1
epoch=1
pkgdesc="Support library for libgcrypt"
arch=('x86_64')
url="https://www.gnupg.org"
source=(ftp://ftp.gnupg.org/gcrypt/libgpg-error/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('64b078b45ac3c3003d7e352a5e05318880a5778c42331ce1ef33d1a0d9922742'
            'SKIP')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA') 

prepare() {
  cd $pkgname-$pkgver

  autoreconf -vif
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
