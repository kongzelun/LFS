# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pcre2
pkgver=10.36
pkgrel=1
epoch=1
pkgdesc="A library that implements Perl 5-style regular expressions. 2nd version"
arch=('x86_64')
url="https://www.pcre.org/"
source=(https://ftp.pcre.org/pub/pcre/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('a9ef39278113542968c7c73a31cfcb81aca1faa64690f400b907e8ab6b4a665c'
            'SKIP')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-jit \
    --enable-pcre2grep-libz \
    --enable-pcre2grep-libbz2 \
    --enable-pcre2test-libreadline

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
