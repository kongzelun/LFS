# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fakechroot
pkgver=2.20.1
pkgrel=1
epoch=1
pkgdesc="Gives a fake chroot environment"
arch=('x86_64')
url="https://github.com/dex4er/fakechroot/wiki"
source=(#$pkgname-$pkgver.tar.gz::https://github.com/dex4er/fakechroot/archive/$pkgver.tar.gz
        git+https://github.com/dex4er/fakechroot.git#commit=b42d1fb9538f680af2f31e864c555414ccba842a)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib/libfakeroot

  make
}

check() {
  cd $pkgname

  make -k check || warning "Check failed."
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
  
  install -dm755 "$pkgdir"/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot/fakechroot/' > "$pkgdir"/etc/ld.so.conf.d/fakechroot.conf
}
