# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=ed
pkgver=1.17
pkgrel=1
epoch=1
pkgdesc="A POSIX-compliant line-oriented text editor"
arch=('x86_64')
url="https://www.gnu.org/software/ed/ed.html"
options=(!emptydirs)
source=(https://ftp.gnu.org/gnu/ed/$pkgname-$pkgver.tar.lz{,.sig})
sha256sums=('71de39883c25b6fab44add80635382a10c9bf154515b94729f4a6529ddcc5e54'
            'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    "CPPFLAGS=$CPPFLAGS" \
    "CFLAGS=$CFLAGS" \
    "LDFLAGS=$LDFLAGS"

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
