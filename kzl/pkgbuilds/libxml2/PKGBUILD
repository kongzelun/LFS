# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libxml2
pkgver=2.9.10+190+gf93ca3e1
pkgrel=1
epoch=1
pkgdesc="XML parsing library, version 2"
arch=('x86_64')
url="http://xmlsoft.org"
depends=(icu)
source=(git+https://gitlab.gnome.org/GNOME/libxml2.git#commit=f93ca3e140a371b26366f747a408588c631e0fd1)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-/+/g' | sed 's/^v//'
}

prepare() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --with-history \
    --with-icu \
    --with-python=/usr/bin/python3 \
    --with-threads

  PYTHONHASHSEED=0 make
  find doc -type f -exec chmod 0644 {} +
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
