# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=openssl
pkgver=1.1.1j
pkgrel=1
epoch=1
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security"
arch=('x86_64')
url="https://www.openssl.org"
backup=(etc/ssl/openssl.cnf)
source=(https://www.openssl.org/source/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('aaf2fcb575cdf6491b98ab4829abf78a3dec8402b8b81efc8f23c00d443981bf'
            'SKIP')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491')

prepare() {
  cd $pkgname-$pkgver

  sed -e "s|./demoCA|/etc/ssl|g" \
      -i apps/openssl.cnf \
      -i apps/CA.pl.in
}

build() {
  cd $pkgname-$pkgver

  ./Configure \
    --prefix=/usr \
    --libdir=lib \
    --openssldir=/etc/ssl \
    shared \
    zlib-dynamic \
    no-ssl3-method \
    enable-ec_nistp_64_gcc_128 \
    linux-x86_64 \
    "-Wa,--noexecstack $CPPFLAGS $CFLAGS $LDFLAGS"

  make depend
  make
}

check() {
  cd $pkgname-$pkgver

  sed -e "s|/etc/ssl|./demoCA|g" \
      -i apps/openssl.cnf \
      -i apps/CA.pl.in

	make test

  sed -e "s|./demoCA|/etc/ssl|g" \
      -i apps/openssl.cnf \
      -i apps/CA.pl.in
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" MANSUFFIX=ssl install_sw install_ssldirs install_man_docs
  chmod 555 "$pkgdir"/etc/ssl/certs/
}
