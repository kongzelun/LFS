# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pacman-contrib
pkgver=1.4.0
pkgrel=1
epoch=1
pkgdesc="Contributed scripts and tools for pacman systems"
arch=('x86_64')
url="https://git.archlinux.org/pacman-contrib.git/about/"
depends=(fakeroot)
optdepends=("mlocate: pacdiff"
            "vim: default merge program for pacdiff")
source=(https://git.archlinux.org/$pkgname.git/snapshot/$pkgname-$pkgver.tar.{gz,asc})
sha256sums=('8746f1352aaad990fe633be34f925d5ab8bd8a19a5f1274e72dbde426deb86bd'
            'SKIP')
validpgpkeys=('04DC3FB1445FECA813C27EFAEA4F7B321A906AD9')

prepare() {
  cd pacman-contrib-$pkgver

  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-doc

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
