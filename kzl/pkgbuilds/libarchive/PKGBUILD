# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libarchive
pkgver=3.5.1
pkgrel=1
epoch=1
pkgdesc="Multi-format archive and compression library"
arch=('x86_64')
url="https://libarchive.org/"
depends=(acl expat lz4 openssl)
source=(https://github.com/$pkgname/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('0e17d3a8d0b206018693b27f08029b598f6ef03600c2b5d10c94ce58692e299b'
            'SKIP')
validpgpkeys=('A5A45B12AD92D964B89EEE2DEC560C81CEC2276E')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --without-xml2 \
    --without-nettle \
    --disable-static

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
