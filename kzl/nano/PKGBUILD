# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=nano
pkgver=5.3
pkgrel=1
epoch=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
url="https://www.nano-editor.org"
backup=(etc/nanorc)
source=($url/dist/v${pkgver::1}/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('c5c1cbcf622d9a96b6030d66409ed12b204e8bc01ef5e6554ebbe6fb1d734352'
            'SKIP')
validpgpkeys=('BFD009061E535052AD0DF2150D28D4D2A0ACE884')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-color \
    --enable-nanorc \
    --enable-multibuffer

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/doc/sample.nanorc "$pkgdir"/etc/nanorc
}
