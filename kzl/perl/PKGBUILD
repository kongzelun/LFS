# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=perl
pkgver=5.32.0
pkgrel=1
epoch=1
pkgdesc="A highly capable, feature-rich programming language"
arch=('x86_64')
url="https://www.perl.org"
depends=(db gdbm)
options=(!purge)
source=(https://www.cpan.org/src/5.0/$pkgname-$pkgver.tar.xz)
sha256sums=('6f436b447cf56d22464f980fac1916e707a040e96d52172984c5d184c09b859b')

build() {
  cd $pkgname-$pkgver

  ./Configure -des \
    -Dprefix=/usr \
    -Dvendorprefix=/usr \
    -Dman1dir=/usr/share/man/man1 \
    -Dman3dir=/usr/share/man/man3 \
    -Dman1ext=1perl \
    -Dman3ext=3perl \
    -Doptimize="$CFLAGS" \
    -Dcccdlflags='-fPIC' \
    -Dldflags="$LDFLAGS" \
    -Dlddlflags="-shared $LDFLAGS" \
    -Duseshrplib \
    -Dusethreads \
    -Dmyuname="kzl" \
    -Dmyhostname="kzl-linux" \
    -Dcf_time="`date -u --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}"`"

  make
}

check() {
  cd $pkgname-$pkgver

  make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
