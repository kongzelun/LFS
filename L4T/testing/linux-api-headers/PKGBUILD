# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> binutils -> glibc

pkgname=linux-api-headers
pkgver=4.9.140
pkgrel=1
epoch=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('any')
url='https://www.kernel.org'
makedepends=(rsync)
source=(https://www.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-$pkgver.tar.{xz,sign})
sha256sums=('f888aef58c2c4d82c81511ad6a4528ee9a8241bb96c05c65e71224988782f943'
            'SKIP')
validpgpkeys=('647F28654894E3BD457199BE38DBBDC86092693E')

build() {
  cd linux-$pkgver

  make mrproper
  make headers_check
}

package() {
  cd linux-$pkgver

  make INSTALL_HDR_PATH="$pkgdir/usr/" headers_install
}
