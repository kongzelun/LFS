# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> binutils -> glibc

pkgname=glibc
pkgver=2.27
pkgrel=1
epoch=1
arch=('aarch64')
url="https://www.gnu.org/software/libc"
depends=(tzdata)
options=(!strip)
source=(https://ftp.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz{,.sig}
        locale-gen::https://git.archlinux.org/svntogit/packages.git/plain/trunk/locale-gen?h=packages/glibc
        locale.gen.txt)
backup=(etc/gai.conf
        etc/locale.gen
        etc/nscd.conf)
install=glibc.install
sha256sums=('5172de54318ec0b7f2735e5a91d908afe1c9ca291fec16b5374d9faadfc1fc72'
            'SKIP'
            '83f108f915863c7ed0338e2d3e8f2e071a531a090ef8f8b2eb3a956a3c4f04d7'
            '0540122fb9340d546702a891bcce344fc3e8f21fe482c2c321b71c2e6b36964e')
validpgpkeys=('BC7C7372637EC10C57D7AA6579C43DFBF1CF2187')

# TODO: '--enable-systemtap'

prepare() {
  cd $pkgname-$pkgver
  
  mkdir build
}

build() {
  cd $pkgname-$pkgver/build

  echo "slibdir=/usr/lib" >> configparms
  echo "rtlddir=/usr/lib" >> configparms
  echo "sbindir=/usr/bin" >> configparms
  echo "rootsbindir=/usr/bin" >> configparms

  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fno-plt/}
  LDFLAGS=${LDFLAGS/,-z,now/}

  ../configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib \
    --enable-shared \
    --disable-profile \
    --enable-static-pie \
    --disable-timezone-tools \
    --enable-stackguard-randomization \
    --enable-bind-now \
    --enable-stack-protector=strong \
    --enable-kernel=4.9.140 \
    --enable-cet \
    --with-pkgversion="KZL $pkgver-$pkgrel" \
    --with-headers=/usr/include

  make
}

check() {
  cd $pkgname-$pkgver/build

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver/build

  make install_root="$pkgdir/" install

  rm -f "$pkgdir"/etc/ld.so.{cache,conf}

  install -dm755 "$pkgdir"/usr/lib/{locale,systemd/system,tmpfiles.d}/
  install -dm755 "$pkgdir"/var/db/nscd/
  install -Dm644 ../nscd/nscd.conf "$pkgdir"/etc/
  install -Dm644 ../nscd/nscd.service "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 ../nscd/nscd.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/

  install -Dm644 ../posix/gai.conf "$pkgdir"/etc/gai.conf

  install -Dm755 "$srcdir"/locale-gen "$pkgdir"/usr/bin/

  # Create /etc/locale.gen
  install -Dm644 "$srcdir"/locale.gen.txt "$pkgdir"/etc/locale.gen
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' ../localedata/SUPPORTED >> "$pkgdir"/etc/locale.gen

  # Provided by libxcrypt
  rm -f "$pkgdir"/usr/include/crypt.h "$pkgdir"/usr/lib/libcrypt.{a,so}
}
