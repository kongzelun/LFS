# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=linux
pkgver=5.4.32
pkgrel=1
pkgdesc="LTS Linux Kernel"
arch=(x86_64)
url="https://www.kernel.org"
options=(!strip)
source=($url/pub/linux/kernel/v${pkgver%%.*}.x/$pkgname-$pkgver.tar.{xz,sign}
        config
        https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files/archive/microcode-20191115.tar.gz)
sha256sums=('192d99fab7a47a537493b6d7eddb52892b98f8ada655c0bbb419b360a995f2b3'
            'SKIP'
            'd0fb28f4997a0c35939845d388bbb5059a7d9456eaf3e4b4b6128f9368e4f5ed'
            '14b26d27be70774948b8cb582e298f5317263b8b8bb1fe8e41260eae54f531dc')
validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'
              '647F28654894E3BD457199BE38DBBDC86092693E')

export KBUILD_BUILD_HOST=kzl-linux
export KBUILD_BUILD_USER=kzl
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

prepare() {
  cd $pkgname-$pkgver

  cp "$srcdir"/config .config

  make nconfig
}

build() {
  cd $pkgname-$pkgver

  make all
}

package() {
  cd $pkgname-$pkgver

  make INSTALL_MOD_PATH="$pkgdir/usr" modules_install

  install -Dm755 arch/x86_64/boot/bzImage "$pkgdir"/boot/vmlinuz-linux

  rm "$pkgdir"/usr/lib/modules/$pkgver-kzl/{build,source}
  cp -a . "$pkgdir"/usr/lib/modules/$pkgver-kzl/build
  ln -s build "$pkgdir"/usr/lib/modules/$pkgver-kzl/source
  cp -a System.map "$pkgdir"/boot/System.map
}
