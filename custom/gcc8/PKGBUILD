# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> binutils -> glibc

pkgname=gcc8
pkgver=8.4.0
_islver=0.21
pkgrel=1
epoch=1
pkgdesc="The GNU Compiler Collection ($pkgver)"
arch=('x86_64')
url="https://gcc.gnu.org"
depends=(libmpc)
provides=(gcc8-libs gcc8-fortran)
conflicts=(gcc8-libs gcc8-fortran)
replaces=(gcc8-libs gcc8-fortran)
options=(!emptydirs)
source=(https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz{,.sig}
        http://isl.gforge.inria.fr/isl-$_islver.tar.xz)
sha256sums=('e30a6e52d10e1f27ed55104ad233c30bd1e99cfb5ff98ab022dc941edd1b2dd4'
            'SKIP'
            '777058852a3db9500954361e294881214f6ecd4b594c00da5eee974cd6a54960')
validpgpkeys=('33C235A34C46AA3FFB293709A328C3A2C3C45C06')

prepare() {
  cd gcc-$pkgver
  
  mkdir -p build

  mv ../isl-$_islver isl
  
  sed -e "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" -i {libiberty,gcc}/configure

  # Do not run fixincludes
  sed -e 's@\./fixinc\.sh@-c true@' -i gcc/Makefile.in

  # Set the default directory name for 64-bit libraries to “lib”
  sed -e '/m64=/s/lib64/lib/' -i.orig gcc/config/i386/t-linux64
  sed -e '/mabi.lp64=/s/lib64/lib/' -i.orig gcc/config/aarch64/t-aarch64-linux
}

build() {
  cd gcc-$pkgver/build

  ../configure \
    --with-pkgversion="KZL $pkgver-$pkgrel" \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib \
    --program-suffix="-${pkgver:0:1}" \
    --enable-shared \
    --disable-multilib \
    --enable-threads=posix \
    --enable-__cxa_atexit \
    --enable-gnu-indirect-function \
    --enable-version-specific-runtime-libs \
    --enable-languages=c,c++,fortran,lto \
    --disable-libssp \
    --enable-default-pie \
    --enable-default-ssp \
    --enable-checking=release \
    --with-system-zlib \
    --with-isl \
    --enable-linker-build-id \
    --with-linker-hash-style=gnu \
    --enable-gnu-unique-object \
    --enable-lto \
    --enable-cet=auto\
    --enable-plugin \
    --disable-libstdcxx-pch \
    --enable-clocale=gnu

  make
}

check() {
  cd gcc-$pkgver/build

  make -k check || warning "Check failed."
}

package() {
  cd gcc-$pkgver/build

  make DESTDIR="$pkgdir/" install

  ln -s gcc-8 "$pkgdir"/usr/bin/cc-8

  rm -rf "$pkgdir"/usr/lib/{libcc1.la,libcc1.so,libcc1.so.0,libcc1.so.0.0.0}
  rm -rf "$pkgdir"/usr/lib/gcc/x86_64-pc-linux-gnu/lib/
  rm -rf "$pkgdir"/usr/share/{info,locale,man}/
}
