# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=ghostscript
pkgver=9.53.3
pkgrel=1
epoch=1
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
url="https://www.ghostscript.com/"
depends=(fontconfig ijs jbig2dec lcms2 libcups libidn libjpeg libpaper libpng libtiff libxt openjpeg2)
provides=(ghostxps ghostpcl)
conflicts=(ghostxps ghostpcl)
replaces=(ghostxps ghostpcl)
source=(https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${pkgver//./}/ghostpdl-$pkgver.tar.gz)
sha256sums=('96d04e4e464bddb062c1774ea895c4f1c1c94e6c4b62f5d32218ebd44dd65ba1')

prepare() {
  cd ghostpdl-$pkgver

  rm -rf cups/libs expat ijs jpeg lcms2mt libpng openjpeg tiff zlib
}

build() {
  cd ghostpdl-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-fontconfig \
    --enable-openjpeg \
    --enable-gtk \
    --disable-compile-inits \
    --enable-dynamic \
    --with-libidn \
    --with-libpaper \
    --with-system-libtiff \
    --with-ijs \
    --without-luratech \
    --with-jbig2dec \
    --with-x \
    --with-drivers=ALL \
    --with-fontpath="/usr/share/fonts/gsfonts/"

  make so-only
}

check() {
  cd ghostpdl-$pkgver

  make check || warning "Check failed."
}

package() {
  cd ghostpdl-$pkgver

  make DESTDIR="$pkgdir/" \
    CUPSSERVERROOT="${pkgdir}$(cups-config --serverroot)" \
    CUPSSERVERBIN="${pkgdir}$(cups-config --serverbin)" \
    soinstall

  ln -s gsc "${pkgdir}"/usr/bin/gs

  rm -r "${pkgdir}"/usr/share/man/de
}
