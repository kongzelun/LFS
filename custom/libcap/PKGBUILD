# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libcap
pkgver=2.44
pkgrel=1
epoch=1
pkgdesc="POSIX 1003.1e capabilities"
arch=('x86_64')
url="https://sites.google.com/site/fullycapable/"
depends=(attr)
provides=(libcap.so)
source=(https://kernel.org/pub/linux/libs/security/linux-privs/libcap2/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('92188359cd5be86e8e5bd3f6483ac6ce582264f912398937ef763def2205c8e1'
            'SKIP')
validpgpkeys=('38A644698C69787344E954CE29EE848AE2CCF3F4')

prepare() {
  cd $pkgname-$pkgver

  sed -i "s/CFLAGS :=/CFLAGS += \$(CPPFLAGS) /" Make.Rules
  sed -i "s/LDFLAGS :=/LDFLAGS +=/" Make.Rules
}

build() {
  cd $pkgname-$pkgver

  make prefix=/usr lib=lib SBINDIR=/usr/bin KERNEL_HEADERS=/usr/include RAISE_SETFCAP=no
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" prefix=/usr lib=lib SBINDIR=/usr/bin KERNEL_HEADERS=/usr/include RAISE_SETFCAP=no install

  install -Dm644 pam_cap/capability.conf "$pkgdir"/usr/share/doc/$pkgname/capability.conf.example
}
