# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=meson
pkgver=0.55.3
pkgrel=1
epoch=1
pkgdesc="High productivity build system"
arch=('any')
url="https://mesonbuild.com/"
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz{,.asc})
sha256sums=('6bed2a25a128bbabe97cf40f63165ebe800e4fcb46db8ab7ef5c2b5789f092a5'
            'SKIP')
validpgpkeys=('95181F4EED14FDF4E41B518D3BF4693BFEEB9428')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

check() (
  cd $pkgname-$pkgver

  export LC_CTYPE=en_US.UTF-8 CPPFLAGS= CFLAGS= CXXFLAGS= LDFLAGS=
  python run_tests.py || warning "Check failed."
)

package() {
  cd $pkgname-$pkgver
  
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

  install -Dm644 data/shell-completions/bash/* -t "${pkgdir}"/usr/share/bash-completion/completions/
  install -Dm644 data/shell-completions/zsh/* -t "${pkgdir}"/usr/share/zsh/site-functions/
}
