# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=python-numpy
pkgver=1.18.2
pkgrel=2
arch=(x86_64)
url="https://www.numpy.org/"
depends=(cblas lapack python)
makedepends=(python-nose cython)
optdepends=('python-nose: testsuite'
            'openblas: faster linear algebra')
checkdepends=(python-pytest)
options=(staticlibs)
source=($pkgname-$pkgver.tar.gz::https://github.com/numpy/numpy/archive/v$pkgver.tar.gz)
sha256sums=('fdf3ed4e260e1af08b6743527634e85f999caba60d2525c1d6f5aea79f5a6d1f')

build() {
  cd numpy-$pkgver
  
  python setup.py build
}

check() {
  cd numpy-$pkgver

  python setup.py install --root="$PWD"/tmp_install --optimize=1

  cd "$PWD"/tmp_install

  PATH="$PWD/usr/bin:$PATH" PYTHONPATH="$PWD/usr/lib/python3.8/site-packages:$PYTHONPATH" python -c 'import numpy; numpy.test()'
}

package() {
  cd numpy-$pkgver
  
  python setup.py install --prefix=/usr --root="$pkgdir/" --optimize=1
}
