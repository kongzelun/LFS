# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=intel-mkl
pkgver=2020.1.217
pkgrel=2
pkgdesc="Intel Math Kernel Library"
arch=(x86_64)
url="https://software.intel.com/en-us/mkl"
source=(http://registrationcenter-download.intel.com/akdlm/irc_nas/tec/16533/l_mkl_$pkgver.tgz
        intel-mkl.sh
        intel-mkl.conf)
sha256sums=('082a4be30bf4f6998e4d6e3da815a77560a5e66a68e254d161ab96f07086066d'
            '2fbb38becd19e4fea9aa7e54055b25f646df74c2e69a420cb3165339aa64d43b'
            '2709c49e28529a592f6213715ba9771c83e3874e526a37371080454e5a8db193')

prepare() {
  cd l_mkl_$pkgver/rpm

  for rpm in *.rpm; do
    bsdtar -xf $rpm
  done
}

package() {
  cd l_mkl_$pkgver/rpm

  install -dm755 "$pkgdir"/usr/bin
  install -Dm755 opt/intel/compilers_and_libraries_$pkgver/linux/mkl/bin/mkl_link_tool "$pkgdir"/opt/intel/mkl/bin/mkl_link_tool
  ln -s /opt/intel/mkl/tools/mkl_link_tool "$pkgdir"/usr/bin/mkl_link_tool

  mkdir -p "$pkgdir"/opt/intel/mkl/lib/intel64
  cp opt/intel/compilers_and_libraries_$pkgver/linux/mkl/lib/intel64_lin/*.{a,so} "$pkgdir"/opt/intel/mkl/lib/intel64
  cp opt/intel/compilers_and_libraries_$pkgver/linux/compiler/lib/intel64_lin/*.{a,so} "$pkgdir"/opt/intel/mkl/lib/intel64


  cp -r opt/intel/compilers_and_libraries_$pkgver/linux/mkl/include "$pkgdir"/opt/intel/mkl/include

  install -Dm755 "$srcdir"/intel-mkl.sh "$pkgdir"/etc/profile.d/intel-mkl.sh
  install -Dm644 "$srcdir"/intel-mkl.conf "$pkgdir"/etc/ld.so.conf.d/intel-mkl.conf
}
