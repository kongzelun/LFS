# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=cmake
pkgver=3.17.1
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=(x86_64)
url="https://www.cmake.org/"
depends=(curl jsoncpp libarchive libuv rhash shared-mime-info)
# makedepends=(qt5)
# optdepends=("qt5: cmake-gui")
source=(https://www.cmake.org/files/v${pkgver%.*}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('3aa9114485da39cbd9665a0bfe986894a282d5f0882b1dea960a739496620727')

build() {
  cd $pkgname-$pkgver

  export CXXFLAGS+=" $CPPFLAGS"

  ./bootstrap \
    --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)

    # TODO: --qt-gui

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  install -dm644 "$pkgdir"/usr/share/bash-completion/completions/
  ln -s /usr/share/$pkgname-${pkgver%.*}/completions/{cmake,cpack,ctest} "$pkgdir"/usr/share/bash-completion/completions/
}
