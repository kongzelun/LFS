# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=binutils
pkgver=2.34
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=(x86_64)
url="https://www.gnu.org/software/binutils/"
options=(staticlibs)
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f00b0e8803dc9bab1e2165bd568528135be734df3fabf8d0161828cd56028952'
            'SKIP')
validpgpkeys=('3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F')

prepare() {
  cd $pkgname-$pkgver

  mkdir build
}

build() {
  cd $pkgname-$pkgver/build

  ../configure \
    --prefix=/usr \
    --enable-gold \
    --enable-ld=default \
    --enable-lto \
    --enable-shared \
    --enable-threads \
    --with-system-zlib

  make configure-host
  make tooldir=/usr
}

check() {
  cd $pkgname-$pkgver/build

  make -k LDFLAGS="" check || true
}

package() {
  cd $pkgname-$pkgver/build

  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr/"  install

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*
}
