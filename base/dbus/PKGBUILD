# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=dbus
pkgver=1.12.16
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
arch=(x86_64)
url="https://wiki.freedesktop.org/www/Software/dbus/"
depends=(expat)
optdepends=(audit)
source=(https://dbus.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('54a22d2fa42f2eb2a871f32811c6005b531b9613b1b93a0d269b05e7549fec80'
            'SKIP')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90'
              '3C8672A0F49637FE064AC30F52A43A1E4B77B059')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/dbus-$pkgver \
    --docdir=/usr/share/doc/dbus-$pkgver \
    --runstatedir=/run \
    --enable-inotify \
    --enable-systemd \
    --enable-user-session \
    --disable-static \
    --disable-doxygen-docs \
    --disable-xml-docs \
    --with-console-auth-dir=/run/console/ \
    --with-dbus-user=dbus \
    --with-system-pid-file=/run/dbus/pid \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --without-x

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  # pre-assigned uid (81)
  echo 'u dbus 81 "System Message Bus"' | install -Dm644 /dev/stdin "$pkgdir"/usr/lib/sysusers.d/dbus.conf
}
