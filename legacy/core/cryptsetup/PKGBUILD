# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=cryptsetup
pkgver=2.3.1
pkgrel=1
pkgdesc="Userspace setup tool for transparent encryption of block devices using dm-crypt"
arch=('x86_64')
url="https://gitlab.com/cryptsetup/cryptsetup/"
depends=(argon2 device-mapper json-c libgcrypt openssl popt)
source=(https://www.kernel.org/pub/linux/utils/cryptsetup/v${pkgver%.*}/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('92aba4d559a2cf7043faed92e0f22c5addea36bd63f8c039ba5a8f3a159fe7d2'
            'SKIP')
validpgpkeys=('2A2918243FDE46648D0686F9D9B0577BD93E98FC')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-libargon2 \
    --disable-static

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
