# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gettext
pkgver=0.20.2
pkgrel=1
pkgdesc="GNU internationalization library"
arch=('x86_64')
url="https://www.gnu.org/software/gettext/"
depends=(acl libunistring libxml2)
options=(!docs)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('ecb9d0908ca41d5ca5fef974323b3bba6bec19eebba0b44f396de98cfcc089f1'
            'SKIP')
validpgpkeys=('9001B85AF9E1B83DF1BDA942F5BE8B267C6A406D')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --without-included-gettext \
    --without-included-libunistring \
    --without-included-libxm \
    --without-emacs

  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i gettext-{tools,runtime,runtime/libasprintf}/libtool

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
