# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=parted
pkgver=3.3
pkgrel=1
pkgdesc="A program for creating, destroying, resizing, checking and copying partitions"
arch=('x86_64')
url="https://www.gnu.org/software/parted/parted.html"
depends=(device-mapper)
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('57e2b4bd87018625c515421d4524f6e3b55175b472302056391c5f7eccb83d44'
            'SKIP')
validpgpkeys=('1B49F933916A37A3F45A1812015F4DD4A70FB705'
              'B4C6B451E4FA8B4232CA191E117E8C168EFE3A7F') 

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --disable-rpath

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
