# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gdb
pkgver=9.1
pkgrel=1
pkgdesc="The GNU Debugger"
arch=(x86_64)
url="https://www.gnu.org/software/gdb"
makedepends=(expat)
source=(https://ftp.gnu.org/gnu/gdb/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('699e0ec832fdd2f21c8266171ea5bf44024bd05164fdf064e4d10cc4cf0d1737'
            'SKIP')
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3')


prepare() {
  cd $pkgname-$pkgver

  mkdir build

  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd $pkgname-$pkgver/build

  ../configure \
    --prefix=/usr \
    --enable-tui \
    --with-system-readline \
    --with-system-zlib \
    --with-expat \
    --with-python=/usr/bin/python3

  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir/" install

  # conflicts with binutils
  rm "$pkgdir"/usr/include/{ansidecl,bfd,bfd_stdint,bfdlink,ctf-api,ctf,diagnostics,dis-asm,plugin-api,symcat}.h
  rm "$pkgdir"/usr/lib/{libbfd,libctf-nobfd,libctf,libopcodes}.a
  rm -rf "$pkgdir"/usr/share/locale/
  rm -rf "$pkgdir"/usr/share/info/
}
