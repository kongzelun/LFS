# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fakeroot
pkgver=1.24
pkgrel=1
pkgdesc="Tool for simulating superuser privileges"
arch=(x86_64)
url="https://packages.debian.org/fakeroot"
source=(http://ftp.debian.org/debian/pool/main/f/$pkgname/${pkgname}_${pkgver}.orig.tar.gz)
sha256sums=('2e045b3160370b8ab4d44d1f8d267e5d1d555f1bb522d650e7167b09477266ed')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib/libfakeroot \
    --disable-static \
    --with-ipc=sysv

  make
}

check() {
  cd $pkgname-$pkgver

  make check || true
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  
  install -dm755 "$pkgdir"/etc/ld.so.conf.d/
  echo "/usr/lib/libfakeroot" > "$pkgdir"/etc/ld.so.conf.d/fakeroot.conf
}
