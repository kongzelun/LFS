# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=findutils
pkgver=4.7.0
pkgrel=1
pkgdesc="GNU utilities to locate files"
arch=('x86_64')
url="https://www.gnu.org/software/findutils/"
source=(https://ftp.gnu.org/pub/gnu/findutils/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c5fefbdf9858f7e4feb86f036e1247a54c79fc2d8e4b7064d5aaa1f47dfa789a'
            'SKIP')
validpgpkeys=('A5189DB69C1164D33002936646502EF796917195')

prepare() {
  cd $pkgname-$pkgver

  # Don't build or install locate because we use mlocate, a secure version of locate.
  sed -e '/^SUBDIRS/s/locate//' -e 's/frcode locate updatedb//' -i Makefile.in
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr

  # don't build locate, but the docs want a file in there.
  make -C locate dblocation.texi
  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
