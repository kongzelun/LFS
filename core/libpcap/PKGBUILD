# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libpcap
pkgver=1.9.1
pkgrel=1
pkgdesc="A system-independent interface for user-level packet capture"
arch=(x86_64)
url="https://www.tcpdump.org/"
depends=(bluez libnl libusb)
source=(https://www.tcpdump.org/release/$pkgname-$pkgver.tar.gz{,.sig}
        mgmt.h)
sha256sums=('635237637c5b619bcceba91900666b64d56ecb7be63f298f601ec786ce087094'
            'SKIP'
            '7c85da5330ce7ecb6934795c02c652b9e344461302cf74804a4692c3e2e1e7e2')
validpgpkeys=('1F166A5742ABB9E0249A8D30E089DEF1D9C15D0D')

prepare() {
  cd $pkgname-$pkgver

  mkdir -p include/bluetooth

  cp "$srcdir"/mgmt.h include/bluetooth/
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-ipv6 \
    --enable-bluetooth \
    --with-libnl

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
