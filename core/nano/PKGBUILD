# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=nano
pkgver=4.9.2
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=(x86_64)
url="https://www.nano-editor.org"
backup=(etc/nanorc)
source=($url/dist/v4/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('d8a25eea942ecee2d57b8e037eb4b28f030f818b78773b8fcb994ed5835d2ef6'
            'SKIP')
validpgpkeys=('8DA6FE7BFA7A418AB3CB2354BCB356DF91009FA7'
              'A7F6A64A67DA09EF92782DD79DF4862AF1175C5B'
              'BFD009061E535052AD0DF2150D28D4D2A0ACE884')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-color \
    --enable-nanorc \
    --enable-multibuffer

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/doc/sample.nanorc "$pkgdir"/etc/nanorc
}
