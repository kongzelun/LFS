# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libssh2
pkgver=1.9.0
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
arch=(x86_64)
url="https://www.libssh2.org"
depends=(openssl)
source=($url/download/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('d5fb8bd563305fd1074dda90bd053fb2d29fc4bce048d182f96eaa466dfadafd'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'
              '914C533DF9B2ADA2204F586D78E11C6B279D5C91')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
