# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libcap-ng
pkgver=0.7.10
pkgrel=1
pkgdesc="Library making programming with POSIX capabilities easier than traditional libcap"
arch=(x86_64)
url="https://people.redhat.com/sgrubb/libcap-ng/"
source=($pkgname-$pkgver.tar.gz::https://github.com/stevegrubb/libcap-ng/archive/v$pkgver.tar.gz)
sha256sums=('c3c156a215e5be5430b2f3b8717bbd1afdabe458b6068a8d163e71cefe98fc32')

prepare() {
  cd $pkgname-$pkgver

  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static \
    --without-python

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
