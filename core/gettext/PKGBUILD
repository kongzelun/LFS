# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gettext
pkgver=0.20.1
pkgrel=1
pkgdesc="GNU internationalization library"
arch=(x86_64)
url="https://www.gnu.org/software/gettext/"
depends=(acl)
options=(!docs)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('66415634c6e8c3fa8b71362879ec7575e27da43da562c798a8a2f223e6e47f5c'
            'SKIP')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871'
              '68D94D8AAEEAD48AE7DC5B904F494A942E4616C2')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-csharp \
    --enable-nls \
    --with-xz \
    --without-included-gettext

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
