# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=iputils
pkgver=20200309
pkgrel=1
pkgdesc="Network monitoring tools, including ping"
arch=('x86_64')
url="http://www.skbuff.net/iputils/"
depends=(libcap libidn2 libunistring libxslt)
source=(git+https://github.com/iputils/iputils.git#commit=f406ac9272885016d43214c415b9a9972cd3dc55)
sha256sums=('SKIP')

build() {
  cd $pkgname

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    -D BUILD_RARPD=true \
    build

  ninja -C build
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir/" meson install -C build
}
