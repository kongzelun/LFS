# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=glib2
pkgver=2.64.2
pkgrel=1
pkgdesc="Low level core library"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/GLib"
depends=(libffi pcre)
source=(git+https://gitlab.gnome.org/GNOME/glib.git#commit=e264e6fcf7f5199e137314b0d1ebca7648613148)
sha256sums=('SKIP')

build() {
  cd glib

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"

  meson setup \
    -Dauto_features=enabled \
    -Dbuildtype=plain \
    -Dwrap_mode=nodownload \
    -Db_lto=true \
    -Db_pie=true \
    -Dprefix=/usr \
    -Dlibexecdir=lib \
    -Dsbindir=bin \
    -Dselinux=disabled \
    -Dman=false \
    -Dgtk_doc=false \
    build

  ninja -C build
}

check() {
  cd glib

  meson test -C build --no-suite flaky --print-errorlogs
}

package() {
  cd glib

  DESTDIR="$pkgdir" meson install -C build

  install -Dm644 *.hook -t "$pkgdir"/usr/share/libalpm/hooks
}
