# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=valgrind
pkgver=3.15.0
pkgrel=1
pkgdesc="Tool to help find memory-management problems in programs"
arch=(x86_64)
url="http://valgrind.org/"
makedepends=(gdb)
# makedepends=(gdb openmpi)
source=(git+https://repo.or.cz/valgrind.git#tag=VALGRIND_${pkgver//./_})
sha256sums=('SKIP')

# TODO: openmpi support

build() {
  cd $pkgname

  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fno-plt/}

  ./autogen.sh

  ./configure \
    --prefix=/usr \
    --enable-lto \
    --enable-only64bit \
    --without-mpicc


  make
}

check() {
  cd $pkgname

  make regtest 
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
