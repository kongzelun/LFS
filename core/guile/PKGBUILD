# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=guile
pkgver=3.0.2
pkgrel=1
pkgdesc="Portable, embeddable Scheme implementation written in C"
arch=(x86_64)
url="https://www.gnu.org/software/guile/"
depends=(gc gmp libffi libunistring)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('08c0e7487777740b61cdd97949b69e8a5e2997d8c2fe6c7e175819eb18444506'
            'SKIP')
validpgpkeys=('3CE464558A84FDC69DB40CFB090B11993D9AEBB5'
              'FF478FB264DE32EC296725A3DDC0F5358812F8F2'
              '4FD4D288D445934E0A14F9A5A8803732E4436885')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  rm "$pkgdir"/usr/lib/libguile-2.?.so.*-gdb.scm
}
