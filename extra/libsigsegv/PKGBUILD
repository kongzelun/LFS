# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libsigsegv
pkgver=2.12
pkgrel=1
arch=('x86_64')
pkgdesc="Page fault detection library"
url="https://www.gnu.org/software/libsigsegv/"
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('3ae1af359eebaa4ffc5896a1aee3568c052c99879316a1ab57f8fe1789c390b6'
            'SKIP')
validpgpkeys=('68D94D8AAEEAD48AE7DC5B904F494A942E4616C2')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-shared

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
