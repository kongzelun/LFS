# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gobject-introspection
pkgver=1.64.1
_glibver=2.64.1
pkgrel=1
pkgdesc="Introspection system for GObject-based libraries"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GObjectIntrospection"
depends=(glib2 python-mako python-markdown)
makedepends=(cairo libffi)
options=(!emptydirs)
source=(git+https://gitlab.gnome.org/GNOME/gobject-introspection.git#tag=1.64.1
        git+https://gitlab.gnome.org/GNOME/glib.git?signed#tag=$_glibver)
sha256sums=('SKIP'
            'SKIP')
validpgpkeys=('923B7025EE03C1C59F42684CF0942E894B2EAFA0')

build() {
  cd $pkgname

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    -D gtk_doc=false \
    -D glib_src_dir="$srcdir/glib" \
    build

  ninja -C build
}

check() {
  cd $pkgname

  meson test -C build
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir/" meson install -C build
}
