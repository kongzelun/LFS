# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=graphite2
pkgver=1.3.14
pkgrel=1
pkgdesc="Reimplementation of the SIL Graphite text processing engine"
arch=('x86_64')
url="https://github.com/silnrsi/graphite"
makedepends=(freetype2)
options=(!emptydirs)
source=(https://github.com/silnrsi/graphite/releases/download/$pkgver/graphite2-$pkgver.tgz)
sha256sums=('f99d1c13aa5fa296898a181dff9b82fb25f6cc0933dbaa7a475d8109bd54209d')

build() {
  cd $pkgname-$pkgver

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DGRAPHITE2_COMPARE_RENDERER=OFF \
    -S . \
    -B build

  cmake --build build
}

check() {
  cd $pkgname-$pkgver

  cmake --build build --target tests
}

package() {
  cd $pkgname-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
}
