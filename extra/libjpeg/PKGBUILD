# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libjpeg
pkgver=2.0.4
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
arch=('x86_64')
url="https://libjpeg-turbo.org/"
makedepends=(nasm)
source=(https://downloads.sourceforge.net/project/libjpeg-turbo/$pkgver/libjpeg-turbo-$pkgver.tar.gz{,.sig})
sha256sums=('33dd8547efd5543639e890efbf2ef52d5a21df81faf41bb940657af916a23406'
            'SKIP')
validpgpkeys=('7D6293CC6378786E1B5C496885C7044E033FDE16')

build() {
  cd libjpeg-turbo-$pkgver

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DWITH_JAVA=0 \
    -DWITH_JPEG8=1 \
    -S . \
    -B build

  cmake --build build
}

check() {
  cd libjpeg-turbo-$pkgver

  cmake --build build --target test
}

package() {
  cd libjpeg-turbo-$pkgver

  cmake --build build --target install -- \
    DESTDIR="$pkgdir/" \
    docdir="/usr/share/doc/$pkgname" \
		exampledir="/usr/share/doc/$pkgname"
}
