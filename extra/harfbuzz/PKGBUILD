# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=harfbuzz
pkgver=2.6.6
pkgrel=1
pkgdesc="OpenType text shaping engine"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/HarfBuzz"
depends=(freetype2 glib2 graphite2 icu)
# makedepends=(cairo gobject-introspection)
# optdepents=("cairo: hb-view program")
# checkdepends=(python-fonttools)
source=(git+https://github.com/harfbuzz/harfbuzz#tag=$pkgver)
sha256sums=('SKIP')

# TODO: --with-cairo

prepare() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --with-freetype \
    --with-glib \
    --with-gobject \
    --with-graphite2 \
    --with-icu

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname

  make TMPDIR="$srcdir/" check
  rm -rf "$srcdir"/tmp*
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
